apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    if (project.hasProperty('compileSdkVersion')) {
        compileSdkVersion rootProject.ext.compileSdkVersion
    } else {
        compileSdkVersion 28
    }


    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 2
        versionName "2.0"
    }

    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
    }
}

// https://developer.android.com/studio/build/maven-publish-plugin
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'org.sufficientlysecure'
                artifactId = 'sshauthentication-api'
                version = android.defaultConfig.versionName

                pom {
                    url = 'https://github.com/open-keychain'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    organization {
                        name = 'Confidential Technologies GmbH'
                        url = 'https://www.cotech.de'
                    }
                }
            }
        }
    }
}
